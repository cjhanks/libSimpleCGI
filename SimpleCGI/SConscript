# vim: filetype=python
from os.path import join

Import('Env')


libSimpleCGISourceRoots = [
    '',
    './common',
    './common/*',
    './fcgi',
    './fcgi/*',
]

if GetOption('wsgi'):
    libSimpleCGISourceRoots.extend([
        './wsgi',
        './wsgi/*',
    ])

libSimpleCGISources = []
libSimpleCGIHeaders = []

for path in libSimpleCGISourceRoots:
  sources = join(path, '*.cpp')
  libSimpleCGISources.extend(Glob(sources))

  headers = join(path, '*.hpp')
  libSimpleCGIHeaders.extend(Glob(headers))

libSimpleCGI = \
  Env.StaticLibrary(
    target='SimpleCGI',
    source=libSimpleCGISources
  )

if GetOption('install_prefix'):
    for header in libSimpleCGIHeaders:
        path = join(GetOption('install_prefix'),
                    'include',
                    'SimpleCGI',
                    str(header))
        Env.Install(path, header)

    path = join(GetOption('install_prefix'),
                'lib')
    Env.Install(path, libSimpleCGI)

Return('libSimpleCGI')
